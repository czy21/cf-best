plugins {
    id 'java-library'
    id 'org.springframework.boot'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.cfbest'
version = '0.1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add("-parameters")
}

dependencyManagement {
    generatedPomCustomization {
        enabled = false
    }
    imports {
        mavenBom "com.sunny:sunny-framework-bom:3.0.0-SNAPSHOT"
    }
}

test {
    useJUnitPlatform()
}
dependencies {
    implementation "org.mapstruct:mapstruct", "org.projectlombok:lombok"
    annotationProcessor "org.mapstruct:mapstruct-processor", "org.projectlombok:lombok", "org.projectlombok:lombok-mapstruct-binding"
    api 'com.sunny:sunny-framework-core'
    api 'com.sunny:sunny-framework-web'
    api 'com.sunny:sunny-framework-db'
    api 'com.sunny:sunny-framework-job'
}


tasks.register('copyWeb', Copy) {
    from "$rootDir/../frontend/build"
    into "$buildDir/resources/main/static/"
}

tasks.register('buildWeb', Exec) {
    workingDir "$rootDir/../frontend/"
    commandLine 'sh', '-c', "yarn install && yarn --ignore-engines build"
}

tasks.named('compileJava') {
    dependsOn('buildWeb')
}

tasks.named('processResources') {
    dependsOn('copyWeb')
}